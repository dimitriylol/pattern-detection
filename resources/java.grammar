compilationUnit : <ws> packageDecl? <ws> importDecl* (<ws> declWithBraces <ws>)* ;

(* .................................................. *)
(* DECLARATIONS *)
(* .................................................. *)

packageDecl : <'package'> <ws> qid <ws> <';'> ;

importDecl : <'import'> <ws> <'static'?> <ws> importId <ws> <';'> ;

importId : qid ('.*')? ;       

declWithBraces : 
        classDecl
    |   interfaceDecl
    |   methodDecl
    ;

declBody : (<ws> inDeclBody <ws>)* ;

inDeclBody :
        declWithBraces 
    |   attrDecl
    |   <ws> &'}' <ws>
    ;

(* CLASS *)

classDecl : mods <ws> <'class'> <ws> id <ws> extends implements <'{'> declBody <'}'> ;

(* INTERFACE *)

interfaceDecl : mods <ws> <'interface'> <ws> id <ws> extends <'{'> declBody <'}'> ;

extends:
        <'extends'> <ws> qid <ws> (<','> <ws> qid <ws>)*
    |   <ws>
    ;

implements:
        <'implements'> <ws> qid <ws> (<','> <ws> qid <ws>)*
    |   <ws>;

(* METHOD *)

methodDecl : mods <ws> type? <ws> id <ws> formalParams <ws> <'{'> methodBody <'}'> ;

methodBody : (<ws> inMethodBody <ws>)* ;

inMethodBody :
        declWithBraces
    |   localVar
    |   <ws>
    ;

formalParams : <'('> declPair* (<ws> <','> <ws> declPair)* <')'> ;

(* ATTRIBUTE *)

attrDecl : mods <ws> declVar ';' ;

(* LOCAL VARIABLE *)

localVar : declVar ';' ;

(* .................................................. *)
(* SMTH *)
(* .................................................. *)

smthNotBraces : #'[^{}]*' ;

mandSmthNotBraces : #'[^{}]+' ;
(* .................................................. *)
(* HELPER *)
(* .................................................. *)

declVar : type <ws> id <ws> (',' <ws> id <ws>)* initVar? ;

declPair : type <ws> id ;

initVar: '=' <ws> initVal <ws> ;

initVal : primitiveVal ;

primitiveVal :
        #'[a-zA-Z0-9.]*'
    |   #'".*"'
    |   #'\'.*\''
    ;

(* TYPE *)
type : qid ;

(* MODIFIER *)

mods : (mod <mandatoryWS>)* ;

mod :
        'public'     
    |   'protected'  
    |   'private'    
    |   'static'     
    |   'abstract'   
    |   'final'      
    |   'strictfp'             
    ;

(* IDENTIFIERS *)

qid : !(mod <mandatoryWS>) #'[a-zA-Z0-9_.]+' ;

id : !(mod <mandatoryWS>) #'[a-zA-Z0-9_]+' ;

(* .................................................. *)
(* WS *)
(* .................................................. *)
    
<ws>: <#'\s*'> ;

<mandatoryWS>: <#'\s+'> ;
